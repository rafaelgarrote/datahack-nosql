{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Cypher in Depth"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "En este notebook vamos a ver en profundidad las distintas opciones que tenemos para buscar nuestros patrones en la base de datos:\n",
    "\n",
    "* Filtrar resultados con la cláusula WHERE\n",
    "* Devolver expresiones con la cláusula RETURN\n",
    "* Agregar datos con los operadores sum, avg, count, min, max\n",
    "* Ordenar los resultados con ORDER BY\n",
    "* Paginar el resultado con SKIP y LIMIT\n",
    "\n",
    "Para empezar importaremos lalibrería de cypher y borraremos todos los nodos y relacciones que hubiera en la base de datos para empezar con el notebook de cero."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cypher\n",
    "%matplotlib inline"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (n) DETACH DELETE n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Para este ejercicio vamos a partir de un grafo que almacena peliculas, actores, directores y sus relaciones:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "CREATE (matrix:Movie { title:\"The Matrix\",released:1997 })\n",
    "CREATE (cloudAtlas:Movie { title:\"Cloud Atlas\",released:2012 })\n",
    "CREATE (forrestGump:Movie { title:\"Forrest Gump\",released:1994 })\n",
    "CREATE (keanu:Person { name:\"Keanu Reeves\", born:1964 })\n",
    "CREATE (robert:Person { name:\"Robert Zemeckis\", born:1951 })\n",
    "CREATE (tom:Person { name:\"Tom Hanks\", born:1956 })\n",
    "CREATE (tom)-[:ACTED_IN { roles: [\"Forrest\"]}]->(forrestGump)\n",
    "CREATE (tom)-[:ACTED_IN { roles: ['Zachry']}]->(cloudAtlas)\n",
    "CREATE (robert)-[:DIRECTED]->(forrestGump)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Como resultado tenemos el siguiente grafo\n",
    "\n",
    "<img src=\"../images/neo4j/cypher21.png\" alt=\"Initial Graph\"/>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Filtrar datos\n",
    "\n",
    "Hasta ahora hemos buscado patrones y hemos devuelto los nodos y relaciones que cumplen dicho patrón. Vamos a ver que opciones tenemos para filtrar el resultado y solo devolver el subconjunto de datos que cumplan unas determinadas condiciones. \n",
    "\n",
    "Estas condiciones se expresan utilizando la cláusula WHERE. Para combinar varias condiciones podemos utilizar los operadores AND, OR, XOR y NOT.\n",
    "\n",
    "En esta primera sentencia vamos a filtrar los nodos películas que nos devuelve el patrón que cumplan la condición de que su atributo título sea igua a \"The Matrix\"."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (m:Movie)\n",
    "WHERE m.title = \"The Matrix\"\n",
    "RETURN m"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Para filtrar podemos utilizar expresiones regulares, >, =, <, IN ..."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name =~ \"K.+\" OR m.released > 2000 OR \"Neo\" IN r.roles\n",
    "RETURN p,r,m"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "También podemos filtrar utilizando patrones"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m)\n",
    "WHERE NOT (p)-[:DIRECTED]->()\n",
    "RETURN p,m"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Devolver resultados\n",
    "\n",
    "Además de nodos, relaciones o pahts, cypher puede devolver expresiones. La expresión más sencilla es un literal, pero también pueden ser:\n",
    "\n",
    "* Números, cadenas.\n",
    "* Arrays como por ejemplo [1,2,3]\n",
    "* Mapas como por ejemplo  {name:\"Tom Hanks\", born:1964, movies:[\"Forrest Gump\",…], count:13}\n",
    "* Las propiedades individuales de un nodo, de una realacción de un mapa se pueden acceder con \".\", como por ejemplo n.name\n",
    "* Los elementos de un array o ventanas de un array tulizando \"[]\", como por ejemplo: \n",
    " * names[0]\n",
    " * movies[1..-1]\n",
    "* Aplicar funciones a los valores devueltos, como por ejemplo: \n",
    " * length(array)\n",
    " * toInteger(\"12\")\n",
    " * substring(\"2014-07-01\",0,4) \n",
    " * coalesce(p.nickname,\"n/a\") \n",
    "* También se le pueden poner etiquetas a los campos devueltos con AS: { name: p.name, label:head(labels(p))} AS person\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (p:Person)\n",
    "RETURN p, p.name AS name, toUpper(p.name), coalesce(p.nickname,\"n/a\") AS nickname,\n",
    "  { name: p.name, label:head(labels(p))} AS person"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (n)\n",
    "RETURN DISTINCT labels(n) AS Labels"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Agregaciones\n",
    "\n",
    "En muchas ocasiones queremos agregar o agrupar la información encontrada. En Cypher realizaremos estas operaciones en clausula RETURN. Cypher soporta la mayoría de las operaciones de agreagación: count, sum, avg, min, max y muchas más.\n",
    "\n",
    "Por ejemplo, para contar el número de personas que hay en la base de datos podemos ejecura la siguiente consulta:\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (:Person)\n",
    "RETURN count(*) AS people"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Los valores nulos se han evitado durante el proceso de conteo. \n",
    "\n",
    "Si queremos agregar valores únicos podemos utilizar DISTINCT:\n",
    "\n",
    "count(DISTINCT role).\n",
    "\n",
    "Las agregaciones funcionan de forma implícita, sólo es necesario especificar que columnas queremos agragar y Cypher utilizará las columnas que no tienen agregación para realizar la agrupacción.\n",
    "\n",
    "El siguiente patrón busca la fecuencia con la que uns actor y un director han trabajado juntos:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)<-[:DIRECTED]-(director:Person)\n",
    "RETURN actor, director, count(*) AS collaborations"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "La función collect() es muy útil para devolver los valores de una agregación en una lista. \n",
    "\n",
    "La siguiente sentencia nos permite recuperar todo el reparto de una película y la función cllect nos ayuda a recuperara a todos los acotres de una película en una lista:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (m:Movie)<-[:ACTED_IN]-(a:Person)\n",
    "RETURN m.title AS movie, collect(a.name) AS cast, count(*) AS actors"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Ordenación y Paginación\n",
    "\n",
    "La ordenación se raliza utilizando la clausula  ORDER BY expression [ASC|DESC]. La expresión puede ser cualquiera que sea computable con el resultado del patrón de búsqueda junto con la clausula RETURN.\n",
    "\n",
    "Si devolvemos RETURN person.name podemos ordenar ORDER BY person.age ASC puesto que ambos son accesibles por la variable person. No podemos ordenar por algo que no ha sido devuelto.\n",
    "\n",
    "La paginación se realiza utilizando las cláusulas SKIP {offset} y LIMIT {count}.\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie)\n",
    "RETURN a, count(*) AS appearances\n",
    "ORDER BY appearances DESC LIMIT 10;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Componer consultas muy laragas\n",
    "\n",
    "\n",
    "Para unir el resultado de varias sentencias con la misma estructura podemos utilizar **UNION [ALL]**.\n",
    "\n",
    "Por ejemplo, la siguiente sentencia une actores y directores:\n",
    "\n",
    "Nota: Las columnas devueltas tienen que tener el mismo alias, en este caso 'type'"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (actor:Person)-[r:ACTED_IN]->(movie:Movie)\n",
    "RETURN actor.name AS name, type(r) AS type, movie.title AS title\n",
    "UNION\n",
    "MATCH (director:Person)-[r:DIRECTED]->(movie:Movie)\n",
    "RETURN director.name AS name, type(r) AS type, movie.title AS title"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "También podemos encadenar fragmentos de un patrón utilizando **WITH**. Cada fragmento tabaja con el resultado del fragmento anterior y alimenta el fragmento siguiente creando una estructura de pipeline. Sólo las columnas declaradas en la cláusula WITH están disponibles en las siguientes partes de la query.\n",
    "\n",
    "La cláusula WITH actua como la cláusula RETURN, sólo que no termina la sentencia, sino que prepara la entrada de la siguiente parte de la consulta. \n",
    "\n",
    "Tanto expresiones, como agregaciones, ordenaciones y paginación se pueden utilizar como en la cláusula RETURN, la única diferencia es que todas las columnas tienen que tener alias. \n",
    "\n",
    "En la siguiente sentencia obtenemos las películas en las que un actor aparece y después filtramos aquellas en las que sólo aparece una vez."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (person:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WITH person, count(*) AS appearances, collect(m.title) AS movies\n",
    "WHERE appearances > 1\n",
    "RETURN person.name, appearances, movies"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
