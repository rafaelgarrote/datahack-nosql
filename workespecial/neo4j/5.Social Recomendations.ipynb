{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Social Recomendations\n",
    "\n",
    "En este notebook vamos a estudiar un caso de uso real de una base de datos de grafos para entender el potencial que nos puede proveer en casos reales.\n",
    "\n",
    "## Talent.net\n",
    "Talent.net es una aplicación de recomendaciones que permite a los usuarios descubrir su propia red profesional e identificar a otros usuarios y su conjunto de habilidades particulares. \n",
    "\n",
    "Los usuarios trabajan en empresas y en proyectos y tienen uno o más intereses o habilidades. \n",
    "\n",
    "Con esta información, Talent.net puede describir la red profesional de un usuario identificando otros suscriptores que comparten sus intereses. \n",
    "\n",
    "Las búsquedas se pueden restringir a la compañía en la que trabaja actualmente del usuario, o extenderala para abarcar toda la base de suscriptores. \n",
    "\n",
    "Talent.net también puede identificar individuos con habilidades específicas que están directa o indirectamente\n",
    "conectados al usuario actual. \n",
    "\n",
    "Dichas búsquedas son útiles cuando se busca a un experto en la una materia para un compromiso laboral.\n",
    "\n",
    "Para entender mejor el modelo de datos de talent.net podemos consultar la siguiente imagen.\n",
    "\n",
    "<img src=\"../images/neo4j/talent1.png\" alt=\"Initial Graph\"/>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "El gráfico de muestra sólo tiene dos compañías, cada una con varios empleados. Un empleado está conectado a la empresa en la que trabaja por una relación WORKS_FOR. Cada empleado está interesado (INTERESTED_IN) en uno o más temas y ha trabajado (TRABAJADO_ON) en uno o más proyectos. Ocasionalmente, los empleados de diferentes compañías pueden trabajar en el mismo proyecto.\n",
    "\n",
    "Con este grafo podemos modelar dos casos de uso:\n",
    "\n",
    "* Dado un usuario inferir sus relaciones sociales, esto es identificar su red social profesional basada en interes y habilidades comunes.\n",
    "* Recomendar trabajadores con los que ha trabajado o que han trabajado con alguien con el que ha trabajado con una competencia concreta.\n",
    "\n",
    "El primer caso de uso ayuda a construir comunidades entorno a intereses comunes y el segundo caso de uso ayuda a identificar personas para roles concretos en un trabajo.\n",
    "\n",
    "Para empezar con el ejemplo, vamos a importar las librerías, borrar la base de datos existente y crear el grafo de ejemplo de la imagen anterior."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cypher\n",
    "%matplotlib inline"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (n) DETACH DELETE n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "CREATE (acme:Company { name:\"Acme, Inc.\" })\n",
    "CREATE (charlie:User { name:\"Charlie\" })\n",
    "CREATE (ben:User { name:\"Ben\" })\n",
    "CREATE (sarah:User { name:\"Sarah\" })\n",
    "CREATE (charlie)-[:WORKS_FOR]->(acme)\n",
    "CREATE (ben)-[:WORKS_FOR]->(acme)\n",
    "CREATE (sarah)-[:WORKS_FOR]->(acme)\n",
    "CREATE (startup:Company { name:\"Startup, Ltd.\" })\n",
    "CREATE (arnold:User { name:\"Arnold\" })\n",
    "CREATE (emily:User { name:\"Emily\" })\n",
    "CREATE (gordon:User { name:\"Gordon\" })\n",
    "CREATE (kate:User { name:\"Kate\" })\n",
    "CREATE (arnold)-[:WORKS_FOR]->(startup)\n",
    "CREATE (emily)-[:WORKS_FOR]->(startup)\n",
    "CREATE (gordon)-[:WORKS_FOR]->(startup)\n",
    "CREATE (kate)-[:WORKS_FOR]->(startup)\n",
    "CREATE (platform:Project { name:\"Next Gen Platform\" })\n",
    "CREATE (quantum:Project { name:\"Quantum Leap\" })\n",
    "CREATE (phoenix:Project { name:\"Phoenix\" })\n",
    "CREATE (charlie)-[:WORKED_ON]->(platform)\n",
    "CREATE (ben)-[:WORKED_ON]->(platform)\n",
    "CREATE (sarah)-[:WORKED_ON]->(platform)\n",
    "CREATE (emily)-[:WORKED_ON]->(platform)\n",
    "CREATE (sarah)-[:WORKED_ON]->(quantum)\n",
    "CREATE (emily)-[:WORKED_ON]->(quantum)\n",
    "CREATE (kate)-[:WORKED_ON]->(quantum)\n",
    "CREATE (arnold)-[:WORKED_ON]->(phoenix)\n",
    "CREATE (kate)-[:WORKED_ON]->(phoenix)\n",
    "CREATE (medicine:Topic { name:\"Medicine\" })\n",
    "CREATE (cars:Topic { name:\"Cars\" })\n",
    "CREATE (rest:Topic { name:\"REST\" })\n",
    "CREATE (graphs:Topic { name:\"Graphs\" })\n",
    "CREATE (java:Topic { name:\"Java\" })\n",
    "CREATE (travel:Topic { name:\"Travel\" })\n",
    "CREATE (design:Topic { name:\"Design\" })\n",
    "CREATE (art:Topic { name:\"Art\" })\n",
    "CREATE (music:Topic { name:\"Music\" })\n",
    "CREATE (drama:Topic { name:\"Drama\" })\n",
    "CREATE (charlie)-[:INTERESTED_IN]->(medicine)\n",
    "CREATE (charlie)-[:INTERESTED_IN]->(cars)\n",
    "CREATE (charlie)-[:INTERESTED_IN]->(graphs)\n",
    "CREATE (ben)-[:INTERESTED_IN]->(rest)\n",
    "CREATE (ben)-[:INTERESTED_IN]->(graphs)\n",
    "CREATE (sarah)-[:INTERESTED_IN]->(rest)\n",
    "CREATE (sarah)-[:INTERESTED_IN]->(graphs)\n",
    "CREATE (sarah)-[:INTERESTED_IN]->(java)\n",
    "CREATE (arnold)-[:INTERESTED_IN]->(rest)\n",
    "CREATE (arnold)-[:INTERESTED_IN]->(graphs)\n",
    "CREATE (arnold)-[:INTERESTED_IN]->(java)\n",
    "CREATE (arnold)-[:INTERESTED_IN]->(travel)\n",
    "CREATE (emily)-[:INTERESTED_IN]->(design)\n",
    "CREATE (emily)-[:INTERESTED_IN]->(art)\n",
    "CREATE (gordon)-[:INTERESTED_IN]->(graphs)\n",
    "CREATE (gordon)-[:INTERESTED_IN]->(music)\n",
    "CREATE (kate)-[:INTERESTED_IN]->(music)\n",
    "CREATE (kate)-[:INTERESTED_IN]->(drama)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1. Inferir relaciones sociales"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (subject:User {name:'Sarah'})\n",
    "MATCH (subject)-[:WORKS_FOR]->(company:Company)<-[:WORKS_FOR]-(person:User),\n",
    "      (subject)-[:INTERESTED_IN]->(interest)<-[:INTERESTED_IN]-(person:User)\n",
    "RETURN person.name AS name,\n",
    "       count(interest) AS score,\n",
    "       collect(interest.name) AS interests\n",
    "ORDER BY score DESC"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "* El primer match busca el sujeto de la búsqueda 'Shara' en los nodos etiquetados como 'User' y asigna el resultado a la variable 'subject'.\n",
    "* El segundo match busca 'Users' que trabajan en la misma compañía y que comparten uno o más intereses. Si el 'subject' de la sentencia es Sarah que trabaja para Acme, entonces Ben se ajusta al patrón dos veces, Bentrabaja en Acme y está interesado en 'graphs' (primera coincidencia) y rest (segunda coincidencia). En el caso de Charlie sólo se ajusta una vez trabaja para Acme y está interesado en 'graphs'.\n",
    "* La cláusula RETURN crea una proyección para los datos que cumplen el patrón. Para cada 'User' encontrado extrae su nombre, cuenta el número de intereses que tiene en común con el 'subject' de la sentencia (le pone el alias 'score') y utilizamos collect para devolvel los intereses comunes en fomra de lista. Para aquellos 'Users' que se ajustan al patrón varias veces (Ben en este caso) collect agrega sus resultados en una sóla fila.\n",
    "* Por último ordenamos los resultados en función del 'score' de forma descendente.\n",
    "\n",
    "El subgrafo que cumple el patrón anterior es el siguiente:\n",
    "\n",
    "<img src=\"../images/neo4j/talent3.png\" alt=\"Initial Graph\"/>\n",
    "\n",
    "La sentencia anterior sólo búsca personas que trabajan en la misma compañía que Sarah, si queremos extender la búsqueda para encontrar personas de otras compañías tendríamos que cambiar la consulta. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (subject:User {name:'Sarah'})\n",
    "MATCH (subject)-[:INTERESTED_IN]->(interest:Topic)<-[:INTERESTED_IN]-(person:User),\n",
    "      (person)-[:WORKS_FOR]->(company:Company)\n",
    "RETURN person.name AS name,\n",
    "       company.name AS company,\n",
    "       count(interest) AS score,\n",
    "       collect(interest.name) AS interests\n",
    "ORDER BY score DESC"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Esta sentencia cambia respecto a la anterior en lo siguiente:\n",
    "* En la sentencia MATCH quitamos la parte del patrón que forzaba a que las personas trabajaran en la misma compañía que el 'subject' de la sentencia. Aunque seguimos buscando a que compañía trabaja cada 'User' porque nos interesa devolverlo en el resultado.\n",
    "* En la cláusula RETURN ahora incluimos los detalles de la compañía en la que trabaja cada 'User'.\n",
    "\n",
    "El subgrafo que cumple el patrón anterior es el siguiente:\n",
    "\n",
    "<img src=\"../images/neo4j/talent4.png\" alt=\"Initial Graph\"/>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 2. Buscar compañeros con intereses particulares\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (subject:User {name:'Sarah'})\n",
    "MATCH p=(subject)-[:WORKED_ON]->(:Project)-[:WORKED_ON*0..2]-(:Project)<-[:WORKED_ON]-(person:User)-[:INTERESTED_IN]->(interest:Topic)\n",
    "WHERE person<>subject AND interest.name IN ['Java', 'Travel', 'Medicine']\n",
    "WITH person, interest, min(length(p)) as pathLength\n",
    "ORDER BY interest.name\n",
    "RETURN person.name AS name,\n",
    "       count(interest) AS score,\n",
    "       collect(interest.name) AS interests,\n",
    "       ((pathLength - 1)/2) AS distance\n",
    "ORDER BY score DESC\n",
    "LIMIT 10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "* El primer MATCH busca el sujeto de la búsqueda 'Shara' en los nodos etiquetados como 'User' y asigna el resultado a la variable 'subject'.\n",
    "* El segundo MATCH encuentra 'Users' que están conectados con el 'subject' o bien porque han trabajado en el mismo 'Project' que el 'subject' o bien porque han trabajado en el mismo 'Project' que 'Users' que han trabajado con el 'subject' en el mismo 'Project'. Por cada 'User' que cumple el patrón, capturamos sus 'Interests'. El resultado lo dejmos en la varibale p.\n",
    "* El resultado del patrón es refinado por la cáusula WHERE que excluye los nodos que coinciden con el 'subject' y se asegura de filtrar dotos los 'Users' que no están interesados en algo que nos interesa en la búsqueda. Para cada resultado qu cumple el patrón, le asignamos el path entero de la coincidencia, es decir el camino entre el 'subject' de la query hasta el 'User' que cumple la búsqueda de sus intereses.\n",
    "* La cláusula WITH se encarga de filtrar los caminos que son redundantes (entre el nodo que representa el 'subject' y el nodo 'User' que tiene el interes concreto puede haber varios caminos), cogiendo sólo los que tienen la distancia mínima. Después enlaza este resultado con la cláusual RETURN.\n",
    "* La cláusula RETURN crea una proyección de los datos, se queda con el nombre de los 'Users' (les asigna el alias 'name'), cuenta los intereses de cada 'User' (les asigna el alias 'score'), agrega los 'Interest' de cada 'User' en una lista y por último calculamos cómo de lejos esta el 'User' que cumple el patrón con el 'subject' de la búsqueda. Para ello le restamos 1 (la relacción INTERESTE_IN del final del patrón) y dividimos por dos (el 'User' está separado del 'subject' por pares de relacciones WORKED_ON).\n",
    "* Por último ordenamos el resultado por 'score'.\n",
    "\n",
    "La segunda sentencia MATCH utiliza una variable que indica como de largo tiene que se el camino entre dos nodos, [:WORKED_ON*0..2], en este caso nos interesan sólo los que tiene como máximo logitud 2.\n",
    "\n",
    "El subgrafo que cumple el patrón anterior es el siguiente:\n",
    "\n",
    "<img src=\"../images/neo4j/talent5.png\" alt=\"Initial Graph\"/>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Vamos a ver más en detalle como funciona la query anterior (consulta la siguiente imagen). El primer paso hace referencia a cada uno de los caminos que cumplen los MATCH y el WHERE. Como podemos ver hay un camino redundante, Charlie aparece directamente relaccionado con Sarah a través del proyecto 'Next Gem Platform' y a través de Emily y el proyecto 'Quantum Leap'.\n",
    "\n",
    "El segundo paso hace referencia al filtrado que se realiza en la cláusula WITH. Obtenemos tripletas con el nombre del usuario, el interés y el camino más corto.\n",
    "\n",
    "El tercer paso representa la cláusula RETURN donde agregamos la información para cada usuario que cumple el patrón.\n",
    "\n",
    "<img src=\"../images/neo4j/talent6.png\" alt=\"Initial Graph\"/>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 3. Añadir la relacción WORKED_WITH"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "La sentencia anterior que busca compañeros de compañeros si se combierte en muy frecuente puede ineteresarnos cambiarla por una relacción ya calculada, puesto que es una sentencia costosa. De esta forma optimizamos el rendimiento para este tipo de búquedas.\n",
    "\n",
    "Para ello vamos a crear una nueva relacción que vamos a llamar WORKED_WITH entre nodos 'User' que nos indica que usuarios han trabajado conjuntamente."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "for name in ['Charlie', 'Ben', 'Sarah', 'Arnold', 'Emily', 'Gordon', 'Kate']:\n",
    "    %%cypher http://neo4j:1234@127.0.0.1:7474/db/data \\\n",
    "    MATCH (subject:User {name: {name}}) \\\n",
    "    MATCH (subject)-[:WORKED_ON]->()<-[:WORKED_ON]-(person:User) \\\n",
    "    WHERE NOT((subject)-[:WORKED_WITH]-(person)) \\\n",
    "    WITH DISTINCT subject, person \\\n",
    "    CREATE UNIQUE (subject)-[:WORKED_WITH]-(person) \\\n",
    "    RETURN subject.name AS startName, person.name AS endName"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Una vez añadida la relacción, nuestr grafo queda de la siguiente manera.\n",
    "\n",
    "<img src=\"../images/neo4j/talent7.png\" alt=\"Initial Graph\"/>\n",
    "\n",
    "Con estos cambios realizados, la query anterior se simplificaría y quedaría de la siguiente forma"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (subject:User {name:'Sarah'})\n",
    "MATCH p=(subject)-[:WORKED_WITH*0..1]-(:Person)-[:WORKED_WITH]-(person:User)-[:INTERESTED_IN]->(interest:Topic)\n",
    "WHERE person<>subject AND interest.name IN ['Java', 'Travel', 'Medicine']\n",
    "WITH person, interest, min(length(p)) as pathLength\n",
    "RETURN person.name AS name,\n",
    "       count(interest) AS score,\n",
    "       collect(interest.name) AS interests,\n",
    "       (pathLength - 1) AS distance\n",
    "ORDER BY score DESC\n",
    "LIMIT 10"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
