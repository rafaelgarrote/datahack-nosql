{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Shakespeare Graph"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Ya hemos aprendido a crear nodos, relaciones, ídices, constraints y además hemos aprendido a realizar consultas con patrones sobre esos datos.\n",
    "\n",
    "Ahora es tu turno.\n",
    "\n",
    "En este ejercicio vamos a crear una representación gráfica de la cadena de valor que rodea al pro\n",
    "ducción y consumo de literatura shakesperiana. Almacenaremos información sobre Shakespeare y algunas de sus obras, junto con detalles de uno de las compañías que recientemente interpretaron alguna de sus obras de teatro, además de un lugar de teatro, también almacenamos algunos datos geoespaciales. Incluso hemos agregado una reseña. En total, el gráfico describe y conecta tres dominios diferentes. \n",
    "\n",
    "En el diagrama hemos distinguido estos tres dominios con relaciones con diferentes formatos: punteados para el dominio literario, sólido para el dominio teatral y discontinuo para el dominio geoespacial.\n",
    "\n",
    "<img src=\"../images/neo4j/cypher51.png\" alt=\"Initial Graph\"/>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Una vez que conocemos los datos que vamos a modelar vamos a crear el grafo.\n",
    "\n",
    "Como siempre los primeros pasos son inportar las librerías que vamos a utilizar y borrar los nodos y relaciones que existiesen previamente en la base de datos."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cypher\n",
    "%matplotlib inline"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (n) DETACH DELETE n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Ahora que tenemos la base de datos vacía vamos a crear el grafo inicial para trabajar sobre él."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "CREATE (shakespeare:Author {firstname:'William', lastname:'Shakespeare'}),\n",
    "(juliusCaesar:Play {title:'Julius Caesar'}),\n",
    "(shakespeare)-[:WROTE_PLAY {year:1599}]->(juliusCaesar),\n",
    "(theTempest:Play {title:'The Tempest'}),\n",
    "(shakespeare)-[:WROTE_PLAY {year:1610}]->(theTempest),\n",
    "(rsc:Company {name:'RSC'}),\n",
    "(production1:Production {name:'Julius Caesar'}),\n",
    "(rsc)-[:PRODUCED]->(production1),\n",
    "(production1)-[:PRODUCTION_OF]->(juliusCaesar),\n",
    "(performance1:Performance {date:20120729}),\n",
    "(performance1)-[:PERFORMANCE_OF]->(production1),\n",
    "(production2:Production {name:'The Tempest'}),\n",
    "(rsc)-[:PRODUCED]->(production2),\n",
    "(production2)-[:PRODUCTION_OF]->(theTempest),\n",
    "(performance2:Performance {date:20061121}),\n",
    "(performance2)-[:PERFORMANCE_OF]->(production2),\n",
    "(performance3:Performance {date:20120730}),\n",
    "(performance3)-[:PERFORMANCE_OF]->(production1),\n",
    "(billy:User {name:'Billy'}),\n",
    "(review:Review {rating:5, review:'This was awesome!'}),\n",
    "(billy)-[:WROTE_REVIEW]->(review),\n",
    "(review)-[:RATED]->(performance1),\n",
    "(theatreRoyal:Venue {name:'Theatre Royal'}),\n",
    "(performance1)-[:VENUE]->(theatreRoyal),\n",
    "(performance2)-[:VENUE]->(theatreRoyal),\n",
    "(performance3)-[:VENUE]->(theatreRoyal),\n",
    "(greyStreet:Street {name:'Grey Street'}),\n",
    "(theatreRoyal)-[:STREET]->(greyStreet),\n",
    "(newcastle:City {name:'Newcastle'}),\n",
    "(greyStreet)-[:CITY]->(newcastle),\n",
    "(tyneAndWear:County {name:'Tyne and Wear'}),\n",
    "(newcastle)-[:COUNTY]->(tyneAndWear),\n",
    "(england:Country {name:'England'}),\n",
    "(tyneAndWear)-[:COUNTRY]->(england),\n",
    "(stratford:City {name:'Stratford upon Avon'}),\n",
    "(stratford)-[:COUNTRY]->(england),\n",
    "(rsc)-[:BASED_IN]->(stratford),\n",
    "(shakespeare)-[:BORN_IN]->(stratford)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Vamos a realizar nuestra primera búsqueda en la base dedatos.\n",
    "\n",
    "## 1. Encontrar el Venue 'Theatre Royal', la ciudad 'Newcastle' y el author 'Shakespeare'"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (theater:Venue {name:'Theatre Royal'}),\n",
    "      (newcastle:City {name:'Newcastle'}),\n",
    "      (bard:Author {lastname:'Shakespeare'})\n",
    "RETURN theater, newcastle, bard"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 2. Para acelerar nuestras búsquedas crear un índice sobre el campo 'name' de los nodos 'Venue' "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "CREATE INDEX ON :Venue(name)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 3. Queremos asegurarnos que el campo 'name' de los nodos 'Country' son únicos"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "CREATE CONSTRAINT ON (c:Country) ASSERT c.name IS UNIQUE"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (theater:Venue {name:'Theatre Royal'}),\n",
    "      (newcastle:City {name:'Newcastle'}),\n",
    "      (bard:Author {lastname:'Shakespeare'}),\n",
    "      (newcastle)<-[:STREET|CITY*1..2]-(theater)<-[:VENUE]-()-[:PERFORMANCE_OF]->()-[:PRODUCTION_OF]->(play)<-[:WROTE_PLAY]-(bard)\n",
    "RETURN DISTINCT play.title AS play"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 5."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (theater:Venue {name:'Theatre Royal'}),\n",
    "      (newcastle:City {name:'Newcastle'}),\n",
    "      (bard:Author {lastname:'Shakespeare'}),\n",
    "      (newcastle)<-[:STREET|CITY*1..2]-(theater)<-[:VENUE]-()-[:PERFORMANCE_OF]->()-[:PRODUCTION_OF]->(play)<-[w:WROTE_PLAY]-(bard)\n",
    "WHERE w.year > 1608\n",
    "RETURN DISTINCT play.title AS play"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 6."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (theater:Venue {name:'Theatre Royal'}),\n",
    "      (newcastle:City {name:'Newcastle'}),\n",
    "      (bard:Author {lastname:'Shakespeare'}),\n",
    "      (newcastle)<-[:STREET|CITY*1..2]-(theater)<-[:VENUE]-()-[p:PERFORMANCE_OF]->()-[:PRODUCTION_OF]->(play)<-[:WROTE_PLAY]-(bard)\n",
    "RETURN play.title AS play, count(p) AS performance_count\n",
    "ORDER BY performance_count DESC"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 7."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cypher http://neo4j:1234@127.0.0.1:7474/db/data\n",
    "MATCH (bard:Author {lastname:'Shakespeare'})-[w:WROTE_PLAY]->(play)\n",
    "WITH play\n",
    "ORDER BY w.year DESC\n",
    "RETURN collect(play.title) AS plays"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
